<?xml version="1.0" encoding="UTF-8"?>
<!--
  Copyright 2002-2005 The Apache Software Foundation or its licensors,
  as applicable.

  Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
-->
<forrest:contract name="siteinfo-meta-navigation" type="nugget" 
  xmlns:forrest="http://apache.org/forrest/templates/1.0">
  <description>
    <p>
      <strong>siteinfo-meta-navigation</strong> will output the html additional navigation targets in head element.
      Implemented forrest 0.8/common. </p>
      <div class="note">
      <div class="label">And why ?</div>
      <div class="content">
        <p>This contract insert <code>&lt;link&gt;</code> HTML elements defined by <a href="http://www.w3.org/TR/1999/REC-html401-19991224/types.html#type-links" class="fork">W3C</a> 
        which can be interpreted by browsers to navigate between page.<br/>
        At the moment, the contract implements NEXT, PREVIOUS, LAST, FIRST, Index and CONTENT pages.
        </p>
      </div>
    </div>
  </description>
  <usage><![CDATA[<forrest:contract name="siteinfo-meta-navigation">
  <forrest:properties contract="nav-section">
    <forrest:property name="nav-section" nugget="get.navigation">
      <jx:import uri="cocoon://#{$cocoon/parameters/getRequest}.navigation.xml"/>
    </forrest:property>
  </forrest:properties>
</forrest:contract>]]></usage>
  <forrest:template xmlns:forrest="http://apache.org/forrest/templates/1.0" 
    format="html" name="siteinfo-meta-navigation" inputFormat="xsl" 
    body="false" head="true">
    <xsl:stylesheet version="1.1" 
      xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
      <!-- ********************************** -->
      <!-- Creates navigation links in header -->
      <!-- ********************************** -->
      <xsl:template name="siteinfo-meta-navigation-head">
        <xsl:param name="nav-main" select="'test'"/>

        <!-- Index and Table of Contents -->
        <xsl:call-template name="navigation-link-index-content"/>

        <!-- NEXT, PREVIOUS... -->
        <xsl:for-each select="$nav-main/navigation/menu/div[@id='menu']//div[@class='current']">
          <!-- NEXT -->
          <xsl:if test="ancestor::node()/following-sibling::*[1]//a">
            <link rel="NEXT" 
              href="{ancestor::node()/following-sibling::*[1]//a/@href}"
              title="{ancestor::node()/following-sibling::*[1]//a}">
            </link>
          </xsl:if>
          <!-- PREVIOUS -->
          <xsl:for-each 
            select="ancestor::node()/preceding-sibling::*[1]//a[not( starts-with( @class, 'base' ) )]">
            <xsl:if test="position()=last()">
              <link rel="PREVIOUS" href="{./@href}">
                <xsl:attribute name="title">
                  <xsl:value-of select="."/>
                </xsl:attribute>
              </link>
            </xsl:if>
          </xsl:for-each>
          <!-- LAST -->
          <xsl:if test="parent::node()/following-sibling::*[last()]/a">
            <link rel="LAST" 
              href="{parent::node()/following-sibling::*[last()]/a/@href}">
              <xsl:attribute name="title">
                <xsl:value-of 
                  select="parent::node()/following-sibling::*[last()]/a" />
              </xsl:attribute>
            </link>
          </xsl:if>
          <!-- FIRST -->
          <xsl:if test="parent::node()/preceding-sibling::*[last()]/a">
            <link rel="FIRST" 
              href="{parent::node()/preceding-sibling::*[last()]/a/@href}">
              <xsl:attribute name="title">
                <xsl:value-of 
                  select="parent::node()/preceding-sibling::*[last()]/a"/>
              </xsl:attribute>
            </link>
          </xsl:if>
        </xsl:for-each>
      </xsl:template>
      
      <!-- extraction of the Index and the table of content in order to, possibly, overide them in skins... -->
      <xsl:template name="navigation-link-index-content">
        <!-- Index ... -->
        <link rel="Index" href="{$root}index.html">
          <xsl:attribute name="title">Index...</xsl:attribute>
        </link>
        <!-- Site map ... -->
        <link rel="CONTENTS" href="{$root}linkmap.html">
          <xsl:attribute name="title">Site map...</xsl:attribute>
        </link>
      </xsl:template>
    </xsl:stylesheet>
  </forrest:template>
</forrest:contract>